import:
	java.awt.BasicStroke
	java.awt.Color
	java.awt.RenderingHints
	java.awt.image.BufferedImage
	
	ch.njol.skript.lang.Variable

effect draw [a[n]] circle [(1Â¦outline [with width %-number%])] on [(image|img)] %object% with (radius|width|height) %number%[[( and|,)] [with] %-object%] [at [[center] location] %-number%( and|,) %-number%] [[and] (store|save) ([[the] result]|it) (in|to) [variable] %-object%]:
	parse:
		if expression-7 is set:
			if expression-7 isn't instance of Variable:
				imagesk parse error "'%expression-7%' is not a variable."
				stop
		continue
	trigger:
		if expression-2 isn't instance of BufferedImage:
			stop
		delay the current effect
		if expression-5 is set:
			set {_x} to expression-5
			set {_x} to {_x} - 1
		else:
			set {_x} to 0
		if expression-6 is set:
			set {_y} to expression-6
			set {_y} to {_y} - 1
		else:
			set {_y} to 0
		if raw expression-7 is set:
			set {_image} to imagesk_copiedImage(expression-2)
		else:
			set {_image} to expression-2
		set {_g} to {_image}.createGraphics()
		if imagesk java color expression-4 is instance of Color:
			{_g}.setColor(imagesk java color expression-4)
		{_g}.setRenderingHint((RenderingHints.KEY_ANTIALIASING!) and (RenderingHints.VALUE_ANTIALIAS_ON!))
		if parse mark is 1:
			if expression-1 is set:
				set {_outlineWidth} to expression-1
			else:
				set {_outlineWidth} to 1
			{_g}.setStroke(new BasicStroke({_outlineWidth}, (BasicStroke.CAP_ROUND!) and 2))
			{_g}.drawOval(({_x} - expression-3), ({_y} - expression-3), (expression-3 * 2) and (expression-3 * 2))
		else:
			{_g}.fillOval(({_x} - expression-3), ({_y} - expression-3), (expression-3 * 2) and (expression-3 * 2))
		{_g}.dispose()
		if raw expression-7 is set:
			imagesk set variable (raw expression-7) to {_image} in event
		continue