import:
	java.awt.image.BufferedImage
	
	ch.njol.skript.lang.Variable

effect (draw|merge) [(image|img)] %object% (on|with) [(image|img)] %object% [at [location] %-number%,[ ]%-number%] [with [rotation] %-number% degree[s] [angle] [using origin location %-number%,[ ]%-number%]] [[and] (store|save) ([[the] result]|it) (in|to) [variable] %-object%]:
	parse:
		if expression-8 is set:
			if expression-8 isn't instance of Variable:
				imagesk parse error "'%expression-8%' is not a variable."
				stop
		continue
	trigger:
		if expression-1 or expression-2 isn't instance of BufferedImage:
			stop
		if expression-3 is set:
			set {_x} to (expression-3) - 1
		else:
			set {_x} to 0
		if expression-4 is set:
			set {_y} to (expression-4) - 1
		else:
			set {_y} to 0
		if raw expression-8 is set:
			set {_image} to imagesk_copiedImage(expression-2)
		else:
			set {_image} to expression-2
		delay the current effect
		set {_g} to {_image}.createGraphics()
		if expression-5 is set:
			if expression-6 and expression-7 are set:
				{_g}.rotate(imagesk_toRadians(expression-5), (expression-6) - 1, (expression-7) - 1)
			else:
				{_g}.rotate(imagesk_toRadians(expression-5), {_x}, {_y})
		{_g}.drawImage(expression-1, {_x}, {_y}, null)
		{_g}.dispose()
		if raw expression-8 is set:
			imagesk set variable raw expression-8 to {_image} in event
		continue