options:
	# If changing this is really important for you, you need to change it in all imagesk files.
	variable: imagesk # {imagesk::xxx}

import:
	java.awt.AlphaComposite
	java.awt.Color
	java.awt.image.BufferedImage
	
	ch.njol.skript.lang.Variable

object property (opacity|transparency):
	return type: number
	get:
		if expression-1 is instance of Color:
			return (expression-1.getAlpha() / 255) * 100
		return
	set number:
		if expression-1 is instance of BufferedImage:
			if raw expression-1 is instance of Variable:
				if change value is set:
					set {_opacity} to (1 / 100) * change value
					if {_opacity} is greater than 1:
						set {_opacity} to 1
					else if {_opacity} is smaller than 0:
						set {_opacity} to 0
					set {_temp} to new BufferedImage(expression-1.getWidth(), expression-1.getHeight(), BufferedImage.TYPE_INT_ARGB!)
					set {_g} to {_temp}.createGraphics()
					{_g}.setComposite(AlphaComposite.getInstance(AlphaComposite.SRC_OVER!, {_opacity}))
					{_g}.drawImage((expression-1), 0, 0, null)
					{_g}.dispose()
					imagesk set variable (raw expression-1) to {_temp} in event
			else:
				imagesk error "The image to be edited must be in a variable! (set image opacity)"
		else if expression-1 is instance of Color:
			if raw expression-1 is instance of Variable:
				if change value is set:
					set {_opacity} to change value
					set {_r} to expression-1.getRed()
					set {_g} to expression-1.getGreen()
					set {_b} to expression-1.getBlue()
					if {{@variable}::useFloatColorMethod} is true:
						set {_r} to (1 / 255) * {_r}
						set {_g} to (1 / 255) * {_g}
						set {_b} to (1 / 255) * {_b}
						
						set {_opacity} to (1 / 100) * {_opacity}
						if {_opacity} is greater than 1:
							set {_opacity} to 1
						else if {_opacity} is smaller than 0:
							set {_opacity} to 0
					else:
						set {_opacity} to (255 / 100) * {_opacity}
						if {_opacity} is greater than 255:
							set {_opacity} to 255
						else if {_opacity} is smaller than 0:
							set {_opacity} to 0
					set {_color} to new Color({_r}, {_g}, {_b}, {_opacity})
					imagesk set variable raw expression-1 to {_color} in event
			else:
				imagesk error "The color to be edited must be in a variable! (set color opacity)"