options:
	variable: imagesk

import:
	org.bukkit.Bukkit
	ch.njol.skript.lang.Variable
	ch.njol.skript.variables.Variables
	ch.njol.skript.util.Color as SkriptColor
	java.lang.String
	java.text.AttributedString
	java.io.File
	java.net.URL
	java.awt.Image
	java.awt.image.BufferedImage
	java.awt.AlphaComposite
	java.awt.Color
	java.awt.Font
	java.awt.BasicStroke
	java.awt.geom.Line2D
	java.awt.geom.Line2D$Float as Line2DFloat
	java.awt.font.TextAttribute
	java.awt.RenderingHints
	java.awt.GraphicsEnvironment
	javax.imageio.ImageIO

on load:
	set {Logger} to Bukkit.getServer().getLogger()

expression [(image|img)] colo[u]r (0¦%-color%|1¦[hex] %-string%|2¦[rgb] [\(]%-number%, %-number%, %-number%[\)])[[,] [with] ((opacity|transparency) %-number% [percent]|%-number% [percent] (opacity|transparency)))]:
	get:
		if parse mark is 0 or 1:
			if parse mark is 0:
				set {_color} to expression-1.getBukkitColor()
			else:
				set {_hex} to expression-2
				replace all "##" with "" in {_hex}
				set {_color} to Color.decode("##%{_hex}%")
			set {_r} to {_color}.getRed()
			set {_g} to {_color}.getGreen()
			set {_b} to {_color}.getBlue()
		else if parse mark is 2:
			set {_r} to expression-3
			set {_g} to expression-4
			set {_b} to expression-5
		loop "r", "g" and "b":
			if {_%loop-value%} is greater than 255:
				set {_%loop-value%} to 255
			else if {_%loop-value%} is smaller than 0:
				set {_%loop-value%} to 0
		if expression-6 is set:
			set {_opacity} to expression-6
		else if expression-7 is set:
			set {_opacity} to expression-7
		if {_opacity} is set:
			set {_opacity} to (255 / 100) * {_opacity}
			if {_opacity} is greater than 255:
				set {_opacity} to 255
			else if {_opacity} is smaller than 0:
				set {_opacity} to 0
			set {_color} to new Color({_r}, {_g}, {_b} and {_opacity})
		else:
			set {_color} to new Color({_r}, {_g} and {_b})
		return {_color}

expression %color% with ((opacity|transparency) %-number% [percent]|%-number% [percent] (opacity|transparency)):
	get:
		return color expression-1 with opacity expression-2

function imgskColor(color: object) :: object:
	if {_color} is instance of SkriptColor:
		return color {_color}
	return {_color}

function imgskSetVariable(variable: object, object: object, event: event):
	set {_variable} to "%{_variable}%"
	if first character of {_variable} is "{":
		if last character of {_variable} is "}":
			set {_variable} to subtext of {_variable} from character 2 to (length of {_variable}) - 1
	if first character of {_variable} is "_":
		Variables.setVariable((subtext of {_variable} from character 2 to length of {_variable}), {_object}, {_event} and true);
	else:
		set {%{_variable}%} to {_object}

function imgskCopyImage(image: object) :: object:
	return new BufferedImage(({_image}.getColorModel()), ({_image}.copyData(null)), ({_image}.isAlphaPremultiplied()) and null)

effect create [a[n]] [new] image with (0¦width %number%[( and|,)] [with] height %number%|1¦height %number%[( and|,)] [with] width %number%)[[( and|,)] [with] b[ack]g[round] %-object%] [and] (store|save) [[the] result] (in|to) [variable] %-object%:
	trigger:
		delay the current effect
		if (raw expression-6) isn't instance of Variable:
			if expression-6 is set:
				{Logger}.severe("[image.sk] '%raw expression-6%' is not a variable.");
			continue	
		if parse mark is 0:
			set {_width} to rounded expression-1
			set {_height} to rounded expression-2
		else if parse mark is 1:
			set {_height} to rounded expression-3
			set {_width} to rounded expression-4
		loop "width" and "height":
			if {_%loop-value%} is smaller than 1:
				set {_%loop-value%} to 1
		set {_image} to new BufferedImage({_width}, {_height} and 2)
		if imgskColor(expression-5) is instance of Color:
			set {_g} to {_image}.createGraphics()
			{_g}.setColor(imgskColor(expression-5));
			{_g}.fillRect(0, 0, {_width} and {_height});
			{_g}.dispose();
		imgskSetVariable((raw expression-6), {_image}, (event))
		continue

effect get [the] (image|img) (of|from) (0¦url %-string%|1¦file %-string%|2¦base[ ]64 %-string%) [and] (store|save) [[the] result] (in|to) [variable] %-object%:
	trigger:
		delay the current effect
		if (raw expression-4) isn't instance of Variable:
			if expression-4 is set:
				{Logger}.severe("[image.sk] '%raw expression-4%' is not a variable.");
			continue	
		if parse mark is 0:
			if expression-1 is set:
				set {_image} to ImageIO.read(new URL(expression-1))
				if {_image}.getType() is not 2:
					set {_newImage} to new BufferedImage(({_image}.getWidth()), ({_image}.getHeight()) and 2)
					set {_g} to {_newImage}.createGraphics()
					{_g}.drawImage({_image}, 0, 0, ({_image}.getWidth()), ({_image}.getHeight()) and null);
					{_g}.dispose();
					imgskSetVariable((raw expression-4), {_newImage}, (event))
				else:
					imgskSetVariable((raw expression-4), {_image}, (event))
		else if parse mark is 1:
			if expression-2 is set:
				set {_path} to expression-2
				replace all "/" and "\" with File.separator! in {_path}
				set {_file} to new File({_path})
				if {_file} is set:
					set {_image} to ImageIO.read({_file})
					if {_image}.getType() is not 2:
						set {_newImage} to new BufferedImage(({_image}.getWidth()), ({_image}.getHeight()) and 2)
						set {_g} to {_newImage}.createGraphics()
						{_g}.drawImage({_image}, 0, 0, ({_image}.getWidth()), ({_image}.getHeight()) and null);
						{_g}.dispose();
						imgskSetVariable((raw expression-4), {_newImage}, (event))
					else:
						imgskSetVariable((raw expression-4), {_image}, (event))
		else if parse mark is 2:
			if expression-3 is set:
				#TODO
				stop
		continue

effect resize [(image|img)] %object% to [sizes] (0¦width %number%[( and|,)] height %number%|1¦height %number%[( and|,)] width %number%) [(with|using) algorithm %-number%] [[and] (store|save) [[the] result] (in|to) [variable] %-object%]:
	trigger:
		delay the current effect
		if raw expression-7 is not set:
			if (raw expression-1) isn't instance of Variable:
				if expression-1 is set:
					{Logger}.severe("[image.sk] The image to be resized must be in a variable!");
				continue
		else:
			if (raw expression-7) isn't instance of Variable:
				if expression-7 is set:
					{Logger}.severe("[image.sk] '%raw expression-7%' is not a variable.");
				continue
		if expression-1 isn't instance of BufferedImage:
			continue
		if parse mark is 0:
			set {_width} to rounded expression-2
			set {_height} to rounded expression-3
		else if parse mark is 1:
			set {_height} to rounded expression-4
			set {_width} to rounded expression-5
		loop "width" and "height":
			if {_%loop-value%} is smaller than 1:
				set {_%loop-value%} to 1
		if expression-6 is 1:
			set {_algorithm} to Image.SCALE_AREA_AVERAGING!
		else if expression-6 is 2:
			set {_algorithm} to Image.SCALE_DEFAULT!
		else if expression-6 is 3:
			set {_algorithm} to Image.SCALE_FAST!
		else if expression-6 is 4:
			set {_algorithm} to Image.SCALE_REPLICATE!
		else if expression-6 is 5:
			set {_algorithm} to Image.SCALE_SMOOTH!
		else:
			set {_algorithm} to Image.SCALE_DEFAULT!
		set {_temp} to expression-1.getScaledInstance({_width}, {_height} and {_algorithm})
		set {_resized} to new BufferedImage({_width}, {_height}, and 2)
		set {_g} to {_resized}.createGraphics()
		{_g}.drawImage({_temp}, 0, 0 and null);
		{_g}.dispose();
		if raw expression-7 is not set:
			imgskSetVariable((raw expression-1), {_resized}, (event))
		else:
			imgskSetVariable((raw expression-7), {_resized}, (event))
		continue

object property (0¦width|1¦height):
	get:
		if expression-1 isn't instance of BufferedImage:
			return
		if parse mark is 0:
			return expression-1.getWidth()
		else if parse mark is 1:
			return expression-1.getHeight()

object property [colo[u]r of] pixel [at [location]] %number%( and|,) %number%:
	get:
		if expression-1 isn't instance of BufferedImage:
			return
		if expression-2 is set:
			if expression-3 is set:
				set {_x} to expression-2
				set {_y} to expression-3
				set {_rgb} to expression-1.getRGB(({_x} - 1) and ({_y} - 1))
				return new Color((bits 16-23 of {_rgb}), (bits 8-15 of {_rgb}) and (bits 0-7 of {_rgb}))
	set:
		if (raw expression-1) is instance of Variable:
			if (imgskColor(change value)) isn't instance of Color:
				stop
			if expression-2 is set:
				if expression-3 is set:
					set {_rgb} to (imgskColor(change value)).getRGB()
					set {_x} to expression-2
					set {_y} to expression-3
					expression-1.setRGB(({_x} - 1), ({_y} - 1) and {_rgb});
					imgskSetVariable((raw expression-1), expression-1, (event))
		else:
			{Logger}.severe("[image.sk] The image to be edited must be in a variable! (set pixel color)");
	delete:
		if (raw expression-1) is instance of Variable:
			if expression-2 is set:
				if expression-3 is set:
					set {_rgb} to new Color(0, 0, 0 and 0)
					set {_rgb} to {_rgb}.getRGB()
					set {_x} to expression-2
					set {_y} to expression-3
					expression-1.setRGB(({_x} - 1), ({_y} - 1) and {_rgb});
					imgskSetVariable((raw expression-1), expression-1, (event))
		else:
			{Logger}.severe("[image.sk] The image to be edited must be in a variable! (delete pixel color)");

object property (opacity|transparency):
	set:
		if expression-1 is instance of BufferedImage:
			if (raw expression-1) is instance of Variable:
				if change value is set:
					set {_opacity} to (1 / 100) * change value
					if {_opacity} is greater than 1:
						set {_opacity} to 1
					else if {_opacity} is smaller than 0:
						set {_opacity} to 0
					set {_temp} to new BufferedImage((expression-1.getWidth()), (expression-1.getHeight()) and 2)
					set {_g} to {_temp}.createGraphics()
					{_g}.setComposite(AlphaComposite.getInstance(AlphaComposite.SRC_OVER! and {_opacity}));
					{_g}.drawImage(expression-1, 0, 0 and null);
					{_g}.dispose();
					imgskSetVariable((raw expression-1), {_temp}, (event))
			else:
				{Logger}.severe("[image.sk] The image to be edited must be in a variable! (set image opacity)");
		else if expression-1 is instance of Color:
			if (raw expression-1) is instance of Variable:
				if change value is set:
					set {_opacity} to change value
					set {_opacity} to (255 / 100) * {_opacity}
					if {_opacity} is greater than 255:
						set {_opacity} to 255
					else if {_opacity} is smaller than 0:
						set {_opacity} to 0
					set {_color} to new Color((expression-1.getRed()), (expression-1.getGreen()), (expression-1.getBlue()) and {_opacity})
					imgskSetVariable((raw expression-1), {_color}, (event))
			else:
				{Logger}.severe("[image.sk] The color to be edited must be in a variable! (set color opacity)");
	get:
		if expression-1 is instance of Color:
			return (expression-1.getAlpha() / 255) * 100

effect (get|extract) part of [(image|img)] %object% (0¦from [location] %-number%( and|,) %-number% to [location] %-number%( and|,) %-number%|1¦between locations %-number%( and|,) %-number% and %-number%( and|,) %-number%) [and] (store|save) [[the] result] (in|to) [variable] %-object%:
	trigger:
		delay the current effect
		if expression-1 isn't instance of BufferedImage:
			continue
		else if (raw expression-10) isn't instance of Variable:
			if expression-10 is set:
				{Logger}.severe("[image.sk] '%raw expression-10%' is not a variable.");
			continue
		if parse mark is 0:
			set {_x1} to expression-2
			set {_y1} to expression-3
			set {_x2} to expression-4
			set {_y2} to expression-5
		else if parse mark is 1:
			set {_x1} to expression-6
			set {_y1} to expression-7
			set {_x2} to expression-8
			set {_y2} to expression-9
		set {_width} to difference between {_x1} and {_x2}
		set {_height} to difference between {_y1} and {_y2}
		imgskSetVariable((raw expression-10), (expression-1.getSubimage(({_x1} - 1), ({_y1} - 1), ({_width} - 1) and ({_height} - 1))), (event))
		continue

effect (draw|merge) [(image|img)] %object% (on|with) [(image|img)] %object% [at [location] %-number%( and|,) %-number%] [[and] (store|save) [[the] result] (in|to) [variable] %-object%]:
	trigger:
		delay the current effect
		if raw expression-5 is not set:
			if (raw expression-2) isn't instance of Variable:
				if expression-2 is set:
					{Logger}.severe("[image.sk] The image to be edited must be in a variable! (merge image)");
				continue
		else:
			if (raw expression-5) isn't instance of Variable:
				if expression-5 is set:
					{Logger}.severe("[image.sk] '%raw expression-5%' is not a variable.");
				continue
		if expression-1 isn't instance of BufferedImage:
			continue
		if expression-2 isn't instance of BufferedImage:
			continue
		if expression-3 is set:
			set {_x} to expression-3
			set {_x} to {_x} - 1
		else:
			set {_x} to 0
		if expression-4 is set:
			set {_y} to expression-4
			set {_y} to {_y} - 1
		else:
			set {_y} to 0
		set {_image} to imgskCopyImage(expression-2)
		set {_g} to {_image}.createGraphics()
		{_g}.drawImage(expression-1, {_x}, {_y} and null);
		{_g}.dispose();
		if raw expression-5 is not set:
			imgskSetVariable((raw expression-2), {_image}, (event))
		else:
			imgskSetVariable((raw expression-5), {_image}, (event))
		continue

effect draw [a] line [(10¦with (flat cap|square edge[s]))] on [(image|img)] %object% [with width %-number%][[( and|,)] [with] %-object%] (0¦from [location] %-number%( and|,) %-number% to [location] %-number%( and|,) %-number%|1¦between locations %-number%( and|,) %-number% and %-number%( and|,) %-number%) [[and] (store|save) [[the] result] (in|to) [variable] %-object%]:
	trigger:
		delay the current effect
		if raw expression-12 is not set:
			if (raw expression-1) isn't instance of Variable:
				if expression-1 is set:
					{Logger}.severe("[image.sk] The image to be edited must be in a variable! (draw line)");
				continue
		else:
			if (raw expression-12) isn't instance of Variable:
				if expression-12 is set:
					{Logger}.severe("[image.sk] '%raw expression-12%' is not a variable.");
				continue
		if expression-1 isn't instance of BufferedImage:
			continue
		if parse mark is 0 or 10:
			set {_x1} to expression-4
			set {_y1} to expression-5
			set {_x2} to expression-6
			set {_y2} to expression-7
		else if parse mark is 1 or 11:
			set {_x1} to expression-8
			set {_y1} to expression-9
			set {_x2} to expression-10
			set {_y2} to expression-11
		set {_image} to imgskCopyImage(expression-1)
		set {_g} to {_image}.createGraphics()
		if expression-2 is set:
			set {_width} to expression-2
		else:
			set {_width} to 1
		if parse mark is 10 or 11:
			{_g}.setStroke(new BasicStroke({_width}));
		else:
			{_g}.setStroke(new BasicStroke({_width}, (BasicStroke.CAP_ROUND!) and 2));
		if imgskColor(expression-3) is instance of Color:
			{_g}.setColor(imgskColor(expression-3));
		{_g}.setRenderingHint((RenderingHints.KEY_ANTIALIASING!) and (RenderingHints.VALUE_ANTIALIAS_ON!));
		{_g}.draw(new Line2DFloat({_x1}, {_y1}, {_x2} and {_y2}));
		{_g}.dispose();
		if raw expression-12 is not set:
			imgskSetVariable((raw expression-1), {_image}, (event))
		else:
			imgskSetVariable((raw expression-12), {_image}, (event))
		continue

effect (create|generate) [a[n]] [(image|img)] (text|string) [(with (id|name)|named)] %string% [[and] with text %-string%]:
	trigger:
		if {{@variable}::texts::%expression-1%} is set:
			delete {{@variable}::texts::%expression-1%::*}
		set {{@variable}::texts::%expression-1%} to true
		if expression-2 is set:
			set {{@variable}::texts::%expression-1%::text} to expression-2
		set {{@variable}::texts::%expression-1%::font} to Font.DIALOG!
		set {{@variable}::texts::%expression-1%::size} to 12

expression [the] [(image|img)] (text|string) [(with (id|name)|named)] %string%:
	get:
		if {{@variable}::texts::%expression-1%} is set:
			return "imgtext:%expression-1%"
	delete:
		delete {{@variable}::texts::%expression-1%} and {{@variable}::texts::%expression-1%::*}

expression [(all [[of] the]|the)] [(image|img)] (text|string)[s]:
	get:
		loop {{@variable}::texts::*}:
			add "imgtext:%loop-index%" to {_list::*}
		return {_list::*}
	delete:
		delete {{@variable}::texts::*}
 
expression (0¦(text|string)|1¦font[[( |-)]family]) of [[(image|img)] (text|string)] %string%:
	get:
		if parse mark is 0:
			return {{@variable}::texts::%expression-1%::text}
		else if parse mark is 1:
			return {{@variable}::texts::%expression-1%::font}
	set:
		if {{@variable}::texts::%expression-1%} is set:
			if parse mark is 0:
				if change value is instance of String:
					set {{@variable}::texts::%expression-1%::text} to change value
			else if parse mark is 1:
				if change value is instance of String:
					set {{@variable}::texts::%expression-1%::font} to change value
	reset:
		if parse mark is 0:
			set {{@variable}::texts::%expression-1%::text} to "Sample Text"
		else if parse mark is 1:
			set {{@variable}::texts::%expression-1%::font} to Font.DIALOG!

expression (0¦bold|1¦italic|2¦underline[d]|3¦strikethrough) [state] of [[(image|img)] (text|string)] %string%:
	get:
		if parse mark is 0:
			return {{@variable}::texts::%expression-1%::bold}
		else if parse mark is 1:
			return {{@variable}::texts::%expression-1%::italic}
		else if parse mark is 2:
			return {{@variable}::texts::%expression-1%::underline}
		else if parse mark is 3:
			return {{@variable}::texts::%expression-1%::strikethrough}
	set:
		if {{@variable}::texts::%expression-1%} is set:
			if change value is true or false:
				if parse mark is 0:
					if change value is true:
						set {{@variable}::texts::%expression-1%::bold} to true
					else:
						delete {{@variable}::texts::%expression-1%::bold}
				else if parse mark is 1:
					if change value is true:
						set {{@variable}::texts::%expression-1%::italic} to true
					else:
						delete {{@variable}::texts::%expression-1%::italic}
				else if parse mark is 2:
					if change value is true:
						set {{@variable}::texts::%expression-1%::underline} to true
					else:
						delete {{@variable}::texts::%expression-1%::underline}
				else if parse mark is 3:
					if change value is true:
						set {{@variable}::texts::%expression-1%::strikethrough} to true
					else:
						delete {{@variable}::texts::%expression-1%::strikethrough}
	reset:
		if parse mark is 0:
			delete {{@variable}::texts::%expression-1%::bold}
		else if parse mark is 1:
			delete {{@variable}::texts::%expression-1%::italic}
		else if parse mark is 2:
			delete {{@variable}::texts::%expression-1%::underline}
		else if parse mark is 3:
			delete {{@variable}::texts::%expression-1%::strikethrough}

expression size of [[(image|img)] (text|string)] %string%:
	get:
		return {{@variable}::texts::%expression-1%::size}
	set:
		if "%change value%" parsed as number is set:
			set {{@variable}::texts::%expression-1%::size} to change value
	reset:
		set {{@variable}::texts::%expression-1%::size} to 12

expression (0¦[foreground]|1¦background) colo[u]r of [[(image|img)] (text|string)] %string%:
	get:
		if parse mark is 0:
			return {{@variable}::texts::%expression-1%::foreground}
		else if parse mark is 1:
			return {{@variable}::texts::%expression-1%::background}
	set:
		if imgskColor(change value) is instance of Color:
			if parse mark is 0:
				set {{@variable}::texts::%expression-1%::foreground} to imgskColor(change value)
			else if parse mark is 1:
				set {{@variable}::texts::%expression-1%::background} to imgskColor(change value)
	reset:
		if parse mark is 0:
			delete {{@variable}::texts::%expression-1%::foreground}
		else if parse mark is 1:
			delete {{@variable}::texts::%expression-1%::background}

effect (write|draw) [[(image|img)] (text|string) [(with (id|name)|named)]] %string% (on|to) [(image|img)] %object% [at [location] %-number%( and|,) %-number%] [[and] (store|save) [[the] result] (in|to) [variable] %-object%]:
	trigger:
		delay the current effect
		if expression-2 isn't instance of BufferedImage:
			continue
		if {{@variable}::texts::%expression-1%} is not set:
			continue
		if raw expression-5 is not set:
			if (raw expression-2) isn't instance of Variable:
				if expression-2 is set:
					{Logger}.severe("[image.sk] The image to be edited must be in a variable! (write text)");
				continue
		else:
			if (raw expression-5) isn't instance of Variable:
				if expression-5 is set:
					{Logger}.severe("[image.sk] '%raw expression-5%' is not a variable.");
				continue
		set {_image} to imgskCopyImage(expression-2)
		set {_g} to {_image}.createGraphics()
		if {{@variable}::texts::%expression-1%::color} is set:
			{_g}.setColor({{@variable}::texts::%expression-1%::color});
		set {_font} to {{@variable}::texts::%expression-1%::font}
		set {_size} to {{@variable}::texts::%expression-1%::size}
		if {_size} is smaller than 1:
			set {_size} to 1
		set {_text} to {{@variable}::texts::%expression-1%::text}
		set {_style} to 0
		if {{@variable}::texts::%expression-1%::bold} is set:
			add Font.BOLD! to {_style}
		if {{@variable}::texts::%expression-1%::italic} is set:
			add Font.ITALIC! to {_style}
		set {_font} to new Font({_font}, {_style} and {_size})
		if {{@variable}::texts::%expression-1%::underline}, {{@variable}::texts::%expression-1%::strikethrough}, {{@variable}::texts::%expression-1%::foreground} or {{@variable}::texts::%expression-1%::background} is set:
			set {_aString} to new AttributedString({_text})
			if {{@variable}::texts::%expression-1%::underline} is set:
				{_aString}.addAttribute((TextAttribute.UNDERLINE!) and (TextAttribute.UNDERLINE_ON!));
			if {{@variable}::texts::%expression-1%::strikethrough} is set:
				{_aString}.addAttribute((TextAttribute.STRIKETHROUGH!) and (TextAttribute.STRIKETHROUGH_ON!));
			if {{@variable}::texts::%expression-1%::foreground} is set:
				{_aString}.addAttribute((TextAttribute.FOREGROUND!) and {{@variable}::texts::%expression-1%::foreground});
			if {{@variable}::texts::%expression-1%::background} is set:
				{_aString}.addAttribute((TextAttribute.BACKGROUND!) and {{@variable}::texts::%expression-1%::background});
			{_aString}.addAttribute((TextAttribute.FONT!) and {_font});
			set {_text} to {_aString}.getIterator()
		if expression-3 is set:
			set {_x} to expression-3
			set {_x} to {_x} - 1
		else:
			set {_x} to 0
		if expression-4 is set:
			set {_y} to expression-4
			set {_y} to {_y} - 1
		else:
			set {_y} to {_g}.getFontMetrics({_font}).getHeight()
		{_g}.setFont({_font});
		{_g}.setRenderingHint((RenderingHints.KEY_ANTIALIASING!) and (RenderingHints.VALUE_ANTIALIAS_ON!));
		{_g}.drawString({_text}, {_x} and {_y});
		{_g}.dispose();
		if raw expression-5 is not set:
			imgskSetVariable((raw expression-2), {_image}, (event))
		else:
			imgskSetVariable((raw expression-5), {_image}, (event))
		continue

effect (save|write) [(image|img)] %object% to [file] %string%:
	trigger:
		delay the current effect
		if expression-1 is set:
			if expression-1 isn't instance of BufferedImage:
				continue
			set {_path} to expression-2
			replace all "/" and "\" with File.separator! in {_path}
			set {_file} to new File({_path})
			if {_file} is not set:
				if expression-2 is set:
					{Logger}.severe("[image.sk] '%raw expression-2%' is not a valid file path.");
				continue
			{_file}.mkdirs();
			set {_fileName} to "%last element of split {_path} at File.separator!%"
			if {_fileName} contains ".":
				set {_extension} to "%last element of split {_fileName} at "".""%"
			else:
				set {_extension} to "png"
			ImageIO.write(expression-1, {_extension} and {_file})
		continue
